
function getDEGBoxPlots
(
	CSV degs,
	Matrix exprMatrix,
	SampleGroupTable sampleGroups,
	CSV geneAnnot,
	string direction,
	record comparison,
	string sectionType,
	boolean labelOutliers = true,
	float cexGroupLabel = 1,
	float height = 7,
	string includeGroups = ""
) -> (Latex report)
{
	// which direction?
	if (direction == "up") {
		degsFiltered = TableQuery(table1 = degs, query='SELECT "ids" FROM table1 WHERE "fc" > 0 LIMIT 28')
		caption = 'Top-28 genes with higher expression in '+comparison.caseG+" than "+comparison.controlG+" samples. Genes ordered by absolute fold-change from top-left to bottom-right."
		sectionTitle = 'Top-28 significant genes with higher expression in '+comparison.caseG+' than '+comparison.controlG+' samples'                         
	} 
	else if (direction == "down") {
		degsFiltered = TableQuery(table1 = degs, query='SELECT "ids" FROM table1 WHERE "fc" < 0 LIMIT 28')                        
		caption = 'Top-28 genes with lower expression in '+comparison.caseG+" than "+comparison.controlG+" samples. Genes ordered by absolute fold-change from top-left to bottom-right."                         
		sectionTitle = 'Top-28 significant genes with lower expression in '+comparison.caseG+' than '+comparison.controlG+' samples'                         
	}
	else {
		std.fail("Invalid direction: "+direction)
	}

	if (includeGroups == "") {
		includeGroups = std.concat(comparison.caseG, comparison.controlG, comparison.otherG, sep=",")
	}
	
	geneBoxplot = ExpressionBoxPlot
	(
  		expr   = exprMatrix,
		sampleGroups = sampleGroups,
		annotation = geneAnnot,
		geneIds = degsFiltered,
		includeGroups = includeGroups,
		height = height,
		nRow = 4,
		nCol = 7,
		labelOutliers=labelOutliers,
		cexSampleLabel=0.3,
		cexGroupLabel=cexGroupLabel,
		caption = caption,
		sectionTitle=sectionTitle, 
		sectionType=sectionType
	)

	return geneBoxplot.document
}